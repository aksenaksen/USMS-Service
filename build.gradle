plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
}

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}-SNAPSHOT"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    if (project.path.startsWith(':service:')) {
        // 서비스 모듈: bootJar 활성화, jar 비활성화
        tasks.named('bootJar') {
            enabled = true
        }
        tasks.named('jar') {
            enabled = false
        }
    } else {
        // 공통 모듈: bootJar 비활성화, jar 활성화
        tasks.named('bootJar') {
            enabled = false
        }
        tasks.named('jar') {
            enabled = true
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
